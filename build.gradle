plugins {
    id 'java-library'
    id 'idea'
    id 'maven-publish'
    id 'net.neoforged.moddev' version '1.0.14'
    id 'com.matthewprenger.cursegradle' version '1.4.0'
    id 'com.modrinth.minotaur' version '2.+'
}

version = "${mod_version}+${minecraft_version}"
group = mod_group_id
base {
    archivesName = artifact_name
}

repositories {
    maven {
        name = "Modrinth"
        url = "https://api.modrinth.com/maven"
    }
    maven {
        name "Modmaven"
        url "https://modmaven.dev"
        content {
            // GrandPower
            includeGroup "dev.technici4n"
            // Tesseract
            includeGroup "net.swedz"
        }
    }
    maven {
        name = "shedaniel"
        url "https://maven.shedaniel.me"
        content {
            includeGroup "me.shedaniel.cloth"
        }
    }
    maven {
        name = "TerraformersMC"
        url = "https://maven.terraformersmc.com/releases/"
        content {
            // EMI
            includeGroup "dev.emi"
        }
    }
    mavenLocal()
    mavenCentral()
}

dependencies {
    implementation "maven.modrinth:modern-industrialization:${modern_industrialization_version}"

    implementation "dev.technici4n:GrandPower:${grandpower_version}"

    implementation "me.shedaniel.cloth:cloth-config-neoforge:${cloth_config_version}"

    if (project.runtime_include_emi.toBoolean()) {
        runtimeOnly "dev.emi:emi-neoforge:${emi_version}+${minecraft_version}"
    }
    compileOnly "dev.emi:emi-neoforge:${emi_version}+${minecraft_version}"

    jarJar(implementation("io.github.llamalad7:mixinextras-neoforge:${mixinextras_version}")) {
        version {
            strictly "[${mixinextras_version},)"
            prefer mixinextras_version
        }
    }

    implementation "net.swedz:tesseract-neoforge:${tesseract_version}"
}

tasks.named('jar') {
    from "LICENSE"
}

task sourceJar(type: Jar) {
    archiveClassifier.set("sources")
    from sourceSets.main.allJava
    from "LICENSE"
}

publishing {
    publications {
        register('mavenJava', MavenPublication) {
            artifactId = project.artifact_name

            artifact("$buildDir/libs/${project.artifact_name}-${project.version}.jar") {
                builtBy build
            }

            artifact(sourceJar) {
                classifier "sources"
            }
        }
    }

    repositories {
        mavenLocal()

        maven {
            credentials {
                username = System.getenv("MODMAVEN_USERNAME")
                password = System.getenv("MODMAVEN_PASSWORD")
            }
            name = "modmaven"
            url = "https://modmaven.dev/artifactory/local-releases/"
        }
    }
}

if (System.getenv("CURSEFORGE_API_KEY")) {
    curseforge {
        apiKey = System.getenv("CURSEFORGE_API_KEY")

        project {
            id = project.curseforge_project
            releaseType = project.release_type
            changelogType = "markdown"
            delegate.changelog = System.getenv("MITWEAKS_CHANGELOG")

            addGameVersion project.minecraft_version
            addGameVersion "NeoForge"
            addGameVersion "Java 21"

            relations {
                requiredDependency "modern-industrialization"
                requiredDependency "tesseract-api-neoforge"
            }

            mainArtifact(tasks.named("jar").flatMap { it.archiveFile })
        }

        options {
            forgeGradleIntegration = false
        }

        afterEvaluate {
            tasks.getByName("curseforge${project.curseforge_project}").dependsOn tasks.jar
        }
    }
}

if (System.getenv("MODRINTH_API_KEY")) {
    modrinth {
        token = System.getenv("MODRINTH_API_KEY")

        projectId = project.modrinth_project
        versionNumber = project.mod_version
        versionName = "${project.mod_name} ${version}"
        versionType = project.release_type
        delegate.changelog = System.getenv("MITWEAKS_CHANGELOG")

        gameVersions = [project.minecraft_version]
        loaders = ["neoforge"]

        dependencies {
            required.project "modern-industrialization"
            required.project "tesseract-api"
        }

        uploadFile = tasks.jar
    }
}

neoForge {
    version = neoforge_version

    mods {
        "${mod_id}" {
            sourceSet sourceSets.main
        }
    }

    runs {
        configureEach {
            mods = [neoForge.mods."${mod_id}"]

            systemProperty 'forge.logging.markers', 'REGISTRIES'

            systemProperty 'forge.logging.console.level', 'debug'

            sourceSet = project.sourceSets.main
        }

        client {
            client()

            gameDirectory.set(project.getLayout().getProjectDirectory().dir('runs/client'))

            programArguments.addAll '--username', 'Swedz', '--uuid', 'a2dca537-693e-4ded-ac4b-c4006dd4d382'

            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id

            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            systemProperty 'mixin.debug.export', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
        }

        server {
            server()

            gameDirectory.set(project.getLayout().getProjectDirectory().dir('runs/server'))

            systemProperty 'forge.enabledGameTestNamespaces', project.mod_id
            programArgument '--nogui'
        }

        data {
            data()

            gameDirectory.set(project.getLayout().getProjectDirectory().dir('runs/data'))

            programArguments.addAll '--mod', project.mod_id, '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()

            systemProperty 'mixin.env.remapRefMap', 'true'
            systemProperty 'mixin.env.refMapRemappingFile', "${buildDir}/createSrgToMcp/output.srg"
            systemProperty 'mixin.debug.export', 'true'
            systemProperty 'mixin.debug.verbose', 'true'
        }
    }
}

tasks.withType(ProcessResources).configureEach {
    var replaceProperties = [
            minecraft_version                     : minecraft_version,
            minecraft_version_range               : minecraft_version_range,
            neoforge_version                      : neoforge_version,
            neoforge_version_range                : neoforge_version_range,
            loader_version_range                  : loader_version_range,
            mod_id                                : mod_id,
            mod_name                              : mod_name,
            mod_version                           : version,
            mod_authors                           : mod_authors,
            mod_description                       : mod_description,
            mod_github                            : mod_github,
            modern_industrialization_version_range: modern_industrialization_version_range,
            tesseract_version_range               : tesseract_version_range,
            emi_version_range                     : emi_version_range
    ]
    inputs.properties replaceProperties

    filesMatching(['META-INF/neoforge.mods.toml']) {
        expand replaceProperties
    }
}

java.toolchain.languageVersion = JavaLanguageVersion.of(21)

sourceSets.main.resources { srcDir 'src/generated/resources' }

tasks.withType(JavaCompile).configureEach {
    options.encoding = 'UTF-8'
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}

processResources {
    duplicatesStrategy = 'exclude'

    exclude(".cache")
}